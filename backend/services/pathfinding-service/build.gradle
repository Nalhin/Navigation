plugins {
  id 'org.springframework.boot' version '2.4.5'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

ext {
  set('testcontainersVersion', "1.15.3")
  set('springCloudVersion', "2020.0.2")
}


sourceSets {
  integrationTest {
    compileClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.main.output
  }
}

configurations {
  integrationTestImplementation.extendsFrom implementation, testImplementation
  integrationTestImplementation.extendsFrom runtimeOnly
}

tasks.register('integrationTest', Test) {
  description = 'Runs integration tests.'
  group = 'verification'

  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  shouldRunAfter test
}

integrationTest {
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}

check.dependsOn integrationTest

jacocoTestReport {
  executionData(file("${project.buildDir}/jacoco/test.exec"),
      file("${project.buildDir}/jacoco/integrationTest.exec"))
}

dependencies {
  implementation project(':libraries:pathfinder')

  implementation 'org.springdoc:springdoc-openapi-ui:1.5.8'

  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.cloud:spring-cloud-starter-consul-discovery'

  implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
  implementation 'org.springframework.boot:spring-boot-starter-web'
  annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

  testImplementation 'org.hamcrest:hamcrest-library:2.2'

  integrationTestImplementation 'org.spockframework:spock-spring:2.0-M4-groovy-3.0'
  integrationTestImplementation 'org.springframework.boot:spring-boot-starter-test'
  integrationTestImplementation 'org.testcontainers:spock'
  integrationTestImplementation 'org.testcontainers:mongodb'
  integrationTestImplementation 'io.rest-assured:rest-assured:4.3.3'
  integrationTestImplementation 'io.rest-assured:json-path:4.3.3'
  integrationTestImplementation 'io.rest-assured:xml-path:4.3.3'
}

dependencyManagement {
  imports {
    mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}
